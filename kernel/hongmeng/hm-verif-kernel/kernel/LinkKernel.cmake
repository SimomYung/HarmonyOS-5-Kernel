# Copyright (c) Huawei Technologies Co., Ltd. 2022-2022. All rights reserved.
# Description: CMakeLists.txt for kernel
# Author: Huawei OS Kernel Lab
# Create: Tue Feb 08 16:04:58 2022

include(HongmengCMakeNG/HMbuild/Targets)

macro(init_flags)
    # Get project cflags from DIRECTORY PROPERTY(set by HMbuild)
    set(__KERNEL_DIR_CFLAGS "--sysroot=${CMAKE_SYSROOT} ")
    string(APPEND __KERNEL_DIR_CFLAGS " ${CMAKE_C_FLAGS} ")
    get_property(__KERNEL_HMBUILD_CFLAGS DIRECTORY PROPERTY COMPILE_OPTIONS)
    list(JOIN  __KERNEL_HMBUILD_CFLAGS  " " __KERNEL_HMBUILD_CFLAGS_STR)
    string(APPEND __KERNEL_DIR_CFLAGS " ${__KERNEL_HMBUILD_CFLAGS_STR} ")
    get_directory_property(__KERNEL_DIR_INCLUDES INCLUDE_DIRECTORIES)
    foreach(__KERNEL_INC ${__KERNEL_DIR_INCLUDES})
        # strip generate expression
        string(GENEX_STRIP "${__KERNEL_INC}"  __KERNEL_INC_STRIPPED)
        if (__KERNEL_INC_STRIPPED)
            string(APPEND __KERNEL_DIR_CFLAGS " -I${__KERNEL_INC_STRIPPED} ")
        endif()
    endforeach()
    hmcmakeng_debug("__KERNEL_DIR_CFLAGS = ${__KERNEL_DIR_CFLAGS}")

    # Because cmakeng automatically set security flags, like
    # -Wl,-z,relro,-z,now  -Wl,-z,noexecstack -Wl,--hash-style=gnu
    # but we need set them,so we only remain CONFIG_HMBUILD_EXTRA_LDFLAGS_BASE
    # and CONFIG_HMBUILD_EXTRA_LDFLAGS
    set(__KERNEL_LINK_FLAGS "${CONFIG_HMBUILD_EXTRA_LDFLAGS_BASE} ")
    string(APPEND __KERNEL_LINK_FLAGS "${CONFIG_HMBUILD_EXTRA_LDFLAGS} ")
    if(CONFIG_HMBUILD_TOOLCHAIN_LLVM)
        string(APPEND __KERNEL_LINK_FLAGS " --ld-path=${CMAKE_LINKER} ")
    endif()
    if(CONFIG_HMBUILD_TOOLCHAIN_BISHENG_MOBILE_CPU)
        # The default tls mode of aarch64-linux-ohos target is emutls. kernel should use elf tls.
        string(APPEND __KERNEL_LINK_FLAGS " --target=${CMAKE_C_COMPILER_TARGET} -Wl,-plugin-opt=-emulated-tls=0 ")
    endif()
    if("${CMAKE_LINKER}" MATCHES "ld\.lld$")
        string(APPEND __KERNEL_LINK_FLAGS " -Wl,--apply-dynamic-relocs ")
    endif()
    if(CONFIG_BIG_ENDIAN)
      string(APPEND __KERNEL_LINK_FLAGS " -Wl,-EB ")
      if(CONFIG_ARM)
        string(APPEND __KERNEL_LINK_FLAGS " -Wl,--be8 ")
      endif()
    else()
      string(APPEND __KERNEL_LINK_FLAGS " -Wl,-EL ")
    endif()

    string(APPEND __KERNEL_LINK_FLAGS "-Wl,-z,noexecstack ")
    if(CONFIG_PIE)
      string(APPEND __KERNEL_LINK_FLAGS "-Wl,-pie ")
      string(APPEND __KERNEL_LINK_FLAGS "-Wl,-z,relro,-z,now ")
    endif()

    if(CONFIG_AARCH64_ERRATUM_843419)
      string(APPEND __KERNEL_LINK_FLAGS "-Wl,--fix-cortex-a53-843419 ")
    endif()
    if(CONFIG_AARCH64_ERRATUM_835769 AND CONFIG_HMBUILD_TOOLCHAIN_GNU)
      string(APPEND __KERNEL_LINK_FLAGS "-Wl,--fix-cortex-a53-835769 ")
    endif()
    if(CONFIG_ARM)
      string(APPEND __KERNEL_LINK_FLAGS "-Wl,-X ")
      string(APPEND __KERNEL_LINK_FLAGS "-Wl,--pic-veneer ")
    endif()
    string(APPEND __KERNEL_LINK_FLAGS "-Wl,-Tkernel.lds ")
    string(APPEND __KERNEL_LINK_FLAGS "-nostdlib ")

    # Kernel LTO can be globally enabled in hm-build system.
    if(CONFIG_HMBUILD_LTO)
      string(APPEND __KERNEL_LINK_FLAGS " -flto ")
    endif()
    if(CONFIG_KOBJ_FSVERITY)
      find_package(HMKCrypto  REQUIRED)
      get_target_property(__KERNEL_KCRYPTO_STATICLIB HMKCrypto::HMKCrypto_static IMPORTED_LOCATION)
    endif()
    find_package(HMKLib  REQUIRED)
    find_package(CompilerLibs  REQUIRED)
    if(CONFIG_SUPPORT_HMFDT)
        find_package(HMFdt  REQUIRED)
    else()
        find_package(Libfdt_sysk  REQUIRED)
    endif()

    find_package(HMKitDemoKit REQUIRED)
    if(CONFIG_HMXVM)
      find_package(HMXvmLib REQUIRED)
      get_target_property(__KERNEL_HMXVM_STATICLIB HMXvmLib::HMXvmLibForKernel_static IMPORTED_LOCATION)
    endif()
    find_package(HMVirtLib QUIET)

    get_target_property(__KERNEL_KLIB_STATICLIB HMKLib::HMKLib_static IMPORTED_LOCATION)
    get_target_property(__KERNEL_HMKIT_DEMOKIT_STATICLIB HMKit::DemoKit_static IMPORTED_LOCATION)
    if(CONFIG_SUPPORT_HMFDT)
        get_target_property(__KERNEL_HMFDT_STATICLIB HMFdt::HMFdt_static IMPORTED_LOCATION)
    else()
        get_target_property(__KERNEL_LIBFDT_STATICLIB Libfdt::Libfdt_sysk_static IMPORTED_LOCATION)
    endif()
    get_target_property(__KERNEL_COMPILERS_STATICLIB CompilerLibs::CompilerLibs_static IMPORTED_LOCATION)

    set(__KERNEL_LINK_LIBS "-Wl,--start-group ")

    if(CONFIG_USE_LIBOPT)
        find_package(HMLibOpt QUIET)
        get_target_property(__KERNEL_LIBOPT_STATICLIB HMLibOpt::HMLibOpt_static IMPORTED_LOCATION)
        string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_LIBOPT_STATICLIB} ")
    endif()

    string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_KLIB_STATICLIB} ")
    string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_KCRYPTO_STATICLIB} ")
    string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_HMKIT_DEMOKIT_STATICLIB} ")
    if(CONFIG_SUPPORT_HMFDT)
        string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_HMFDT_STATICLIB} ")
    else()
        string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_LIBFDT_STATICLIB} ")
    endif()
    string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_COMPILERS_STATICLIB} ")
    string(APPEND __KERNEL_LINK_LIBS "${__KERNEL_HMXVM_STATICLIB} ")
    string(APPEND __KERNEL_LINK_LIBS "-Wl,--end-group ")

    hmcmakeng_debug("__KERNEL_LINK_FLAGS  = ${__KERNEL_LINK_FLAGS}")
    hmcmakeng_debug("__KERNEL_LINK_LIBS  = ${__KERNEL_LINK_LIBS}")
endmacro()

macro(build_kernel_link_target target)
    set(HMBUILD_KERNEL_LINKSCRIPT_AR "${CMAKE_AR}")
    set(HMBUILD_KERNEL_LINKSCRIPT_COMPILER "${CMAKE_C_COMPILER}")
    set(HMBUILD_KERNEL_LINKSCRIPT_NM "${CMAKE_NM}")
    set(HMBUILD_KERNEL_LINKSCRIPT_STRIP "${CMAKE_STRIP}")
    set(HMBUILD_KERNEL_LINKSCRIPT_OBJDUMP "${CMAKE_OBJDUMP}")
    set(HMBUILD_KERNEL_LINKSCRIPT_KSYM_CFLAGS "${__KERNEL_DIR_CFLAGS} -D__ASSEMBLY__")
    if (CONFIG_HMBUILD_TOOLCHAIN_BISHENG_MOBILE_CPU)
        # The default tls mode of aarch64-linux-ohos target is emutls. kernel should use elf tls.
        string(APPEND HMBUILD_KERNEL_LINKSCRIPT_KSYM_CFLAGS " --target=${CMAKE_C_COMPILER_TARGET} -Wl,-plugin-opt=-emulated-tls=0 ")
    endif()
    if(CONFIG_KERNEL_MERGE_BUILD)
        string(APPEND HMBUILD_KERNEL_LINKSCRIPT_KSYM_CFLAGS " --hmjam-nojam")
    endif()
    set(HMBUILD_KERNEL_LINKSCRIPT_LDS_CFLAGS "${__KERNEL_DIR_CFLAGS}  -P  -D__ASSEMBLY__ -DLINKER_SCRIPT ")
    if (CONFIG_HMBUILD_TOOLCHAIN_BISHENG_MOBILE_CPU)
        string(APPEND HMBUILD_KERNEL_LINKSCRIPT_LDS_CFLAGS " --target=${CMAKE_C_COMPILER_TARGET} ")
    endif()
    set(HMBUILD_KERNEL_LINKSCRIPT_LDS_SOURCE "${CMAKE_SOURCE_DIR}/arch/${CONFIG_ARCH}/kernel/kernel.lds.S")
    set(HMBUILD_KERNEL_LINKSCRIPT_LDFLAGS "${__KERNEL_LINK_FLAGS}")
    set(HMBUILD_KERNEL_LINKSCRIPT_LINK_OBJS "${CMAKE_BINARY_DIR}/libbuiltin.dev.a")
    set(HMBUILD_KERNEL_LINKSCRIPT_LINK_JAMOBJS "${HMBUILD_KERNEL_LINKSCRIPT_LINK_OBJS}")
    if(CONFIG_KERNEL_MERGE_BUILD)
        set(HMBUILD_KERNEL_LINKSCRIPT_LINK_JAMOBJS "${CMAKE_BINARY_DIR}/kernel.jam.a")
    endif()
    set(HMBUILD_KERNEL_LINKSCRIPT_LINK_LIBS "${__KERNEL_LINK_LIBS}")
    set(HMBUILD_KERNEL_LINKSCRIPT_MERGE_FLAG "")
    if(CONFIG_KERNEL_MERGE_BUILD)
        set(HMBUILD_KERNEL_LINKSCRIPT_MERGE_FLAG "--hmjam-enable")
    endif()
    hmcmakeng_debug("HMBUILD_KERNEL_LINKSCRIPT_KSYM_CFLAGS  = ${HMBUILD_KERNEL_LINKSCRIPT_KSYM_CFLAGS}")
    hmcmakeng_debug("HMBUILD_KERNEL_LINKSCRIPT_LDFLAGS  = ${HMBUILD_KERNEL_LINKSCRIPT_LDFLAGS}")
    hmcmakeng_debug("HMBUILD_KERNEL_LINKSCRIPT_LINK_OBJS  = ${HMBUILD_KERNEL_LINKSCRIPT_LINK_OBJS}")
    hmcmakeng_debug("HMBUILD_KERNEL_LINKSCRIPT_LINK_JAMOBJS  = ${HMBUILD_KERNEL_LINKSCRIPT_LINK_JAMOBJS}")
    hmcmakeng_debug("HMBUILD_KERNEL_LINKSCRIPT_LINK_LIBS  = ${HMBUILD_KERNEL_LINKSCRIPT_LINK_LIBS}")
    configure_file(${CMAKE_SOURCE_DIR}/scripts/link-kernel.sh.in
                   ${CMAKE_BINARY_DIR}/scripts/link-kernel.sh @ONLY)

    hmbuild_target_cmakename_of(builtin_tgt kernel_builtin)
    add_custom_target(${target} ALL
                      BYPRODUCTS kernel.elf
                      COMMAND ${BASH} -c "${CMAKE_BINARY_DIR}/scripts/link-kernel.sh"
                      DEPENDS ${builtin_tgt}
                     )
endmacro()

macro(kernel_link)
    init_flags()
    build_kernel_link_target(link_kernel)
endmacro()
